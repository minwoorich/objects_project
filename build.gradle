plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.asciidoctor.jvm.convert" version '3.3.2'
	id "com.gorylenko.gradle-git-properties" version "2.4.1" //git info
}

group = 'com.objects'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
	testImplementation 'org.projectlombok:lombok:1.18.28'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// RestDocs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	//Querydsl 추가
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation("org.springframework.security:spring-security-test")

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// redis
	implementation 'org.springframework.session:spring-session-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// WebClient 사용시 필요
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Apache POI //category데이터 excel에서 db로 등록시 필요
	implementation 'org.apache.poi:poi:4.1.2'
	implementation 'org.apache.poi:poi-ooxml:4.1.2' // 1.19	버전의 취약점으로 1.21버전 추가 설치
	implementation 'org.apache.commons:commons-compress:1.21'

	//LocalDateTime 처리 라이브러리
//	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.json:json:20231013'

	//hibernate 5
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5-jakarta'

	implementation 'org.springframework.boot:spring-boot-starter-actuator' // 액츄에이터 추가
	implementation 'io.micrometer:micrometer-registry-prometheus' // 프로메테우스 추가
}

ext { // 전역 변수
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'

	sourceDir('src/docs/asciidoc')
	sources('index.adoc')
	outputDir('src/main/resources/static/docs')

	baseDirFollowsSourceDir() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다
	dependsOn test
}

//// static/docs 폴더 비우기
//asciidoctor.doFirst {
//	delete file('src/main/resources/static/docs')
//}
//
//// asccidoctor 작업 이후 생성된 HTML 파일을 static/docs 로 copy
//task copyDocument(type: Copy) {
//	dependsOn asciidoctor
//	from file("build/docs/asciidoc")
//	into file("src/main/resources/static/docs")
//}
//
//// build 의 의존작업 명시
//build {
//	dependsOn copyDocument
//}

bootJar {
	dependsOn asciidoctor
}

def querydslDir = "src/main/generated"

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
//	options.compilerArgs.add("parameters")
}

clean.doLast {
	file(querydslDir).deleteDir()
}


//tasks.withType(JavaCompile) {
//	options.compilerArgs << "-parameters"
//}

// 액츄에이터 빌드 정보 추가
springBoot {
	buildInfo()
}